#include "hsp3utf.as"
#include "modclbk3.hsp"

#include "./SubSource/FunctionDefinition.hsp"
#include "./SubSource/key.hsp"

//ライブラリ的なモジュール
#include "./modules/BrowseFolderMod.as"
#include "./modules/ImageFileModule2.as"
#include "./modules/isfilled.hsp"
#include "./modules/pipe2_utf.hsp"
#include "./modules/TsubuyakiSoup_mod_utf.hsp"

//自分で作ったライブラリ的なモジュール
#include "./modules/DateTimeFormatMod.as"
#include "./modules/encryption.as"
#include "./modules/execwMod.as"
//#include "./modules/i18n.as" //まだ準備ができていないため
#include "./modules/iniMod.as"
#include "./modules/kmodule.as"
#include "./modules/SaveFileDiag_mod.hsp"
#include "./modules/SearchKanCollePos.as"

//どちらというとサブソースなモジュール
#include "./modules/FFMPEGLog.hsp"
#include "./modules/GetAudioDeviceMod.as"
#include "./modules/isHomepostModule.as"
#include "./modules/makeBackgroundImageMod.hsp"
#include "./modules/SelectWindowMod.hsp"

#pack "data2.png"
#pack "huki.png"
#packopt name "艦これ一覧めいかー改二"
#packopt hide 1
#packopt manifest "KanColleListMakerKaini.manifest"
#packopt icon "KanColleListMakerKaini.ico"
#packopt lang "1041"

#define SOFTNAME "一覧めいかー改二 Ver0.8"
#define LONGSOFTNAME "艦これ一覧めいかー改二 Ver0.8"
#define klm2ver 8000

#ifdef _debug
	SafeDelete@kmodule("./SubSource/hsptmp")
	SafeDelete@kmodule("./SubSource/obj")
	SafeDelete@kmodule("./SubSource/packfile")

	SafeDelete@kmodule("./modules/hsptmp")
	SafeDelete@kmodule("./modules/obj")
	SafeDelete@kmodule("./modules/packfile")

	SafeDelete@kmodule("./hsptmp")
	SafeDelete@kmodule("./obj")
	SafeDelete@kmodule("./start.ax")
	SafeDelete@kmodule("./packfile")

	//デバッグ中でも高DPIに対応させる
	SetProcessDPIAware
	//onerror gosub *err_
#endif

	onexit gosub *exit

	wndInfoInit
	nidStackInit
	DAControlInit
	isHomeport_init WND_HOME_PORT_MOD
	//Init@i18n //準備ができていないため

	gosub *DeclaringVariables
	gosub *iniLoad
	gosub *dataCheck
	gosub *iniSave
	gosub *VideoCapSetting

	screen WND_MAIN, disInfo(2), disInfo(3), 2:title SOFTNAME
		SET_WND_INFO
		GetClassLong WND_INFO(WND_MAIN, WI_HANDLE), -26
		SetClassLong WND_INFO(WND_MAIN, WI_HANDLE), -26, stat | $0008
		GetWindowLong WND_INFO(WND_MAIN, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_MAIN, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_MAIN, WI_HANDLE), 0x00000000, 255, LWA_ALPHA
		DragAcceptFiles WND_INFO(WND_MAIN, WI_HANDLE), 1
		oncmd gosub *MainWindowProc, WM_DROPFILES
		oncmd gosub *MainWindowProc, WM_COMMAND
		oncmd gosub *MainWindowProc, WM_HOTKEY
		oncmd gosub *MainWindowProc, WM_TIMER
		oncmd gosub *MainWindowProc, WM_VSCROLL
		oncmd gosub *MainWindowProc, WM_DPICHANGED
		oncmd gosub *MainWindowProc, WM_LBUTTONDOWN
		oncmd gosub *MainWindowProc, WM_LBUTTONDBLCLK
		oncmd gosub *mainWindowProc, WM_SELWINDOW
		objmode 2

		combox dummy, 100, "  SSキャプチャモード\n  艦娘一覧モード\n  攻略編成モード\n  その他一覧作成モード\n  オプション"
		comboxId = stat
		hCombox = objinfo_hwnd(comboxId)

		combox dummy, 100, " 環境設定\n キャプチャ・作成設定\n 動画キャプチャ設定\n Twitter設定\n その他設定"
		optionCBId = stat
		hOptionComBox = objinfo(optionCBId, 2)

		chkbox "dummy", dummy

	screen WND_GRID, disInfo(2), disInfo(3), 2:title "表示させたいセルにD&D"
		SET_WND_INFO
		DragAcceptFiles WND_INFO(WND_GRID, WI_HANDLE), 1
		GetWindowLong WND_INFO(WND_GRID, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_GRID, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_GRID, WI_HANDLE), 0x00000000, 255, LWA_ALPHA
		oncmd gosub *GridWindowProc, WM_DROPFILES
		oncmd gosub *GridWindowProc, WM_SIZING
		oncmd gosub *GridWindowProc, WM_LBUTTONDOWN
		oncmd gosub *GridWindowProc, WM_RBUTTONDOWN
		framesx = ginfo_sizex - ginfo_winx
		framesy = ginfo_sizey - ginfo_winy
		Init@kmodule

	//範囲選択用レイヤードウィンドウ
	bgscr WND_OVERLAY, disInfo(2), disInfo(3), 2
		SET_WND_INFO
		color 0,255,0
		boxf
		GetWindowLong WND_INFO(WND_OVERLAY, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_OVERLAY, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_OVERLAY, WI_HANDLE), 0x00000000, 128, LWA_ALPHA

	bgscr WND_SUPPORT, 850, 100, 2
		SET_WND_INFO
		oncmd gosub *SupportWindowProc, WM_LBUTTONDOWN
		GetWindowLong WND_INFO(WND_SUPPORT, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_SUPPORT, WI_HANDLE),-20, stat | 0x00080008
		SetLayeredWindowAttributes WND_INFO(WND_SUPPORT, WI_HANDLE), 0x000000FF, 255, LWA_COLORKEY
		gosub *DrawSupportWnd

	//画像読み込み用
	buffer WND_IMAGE_BUF
		SET_WND_INFO
		ImgF_PicloadEx "data2.png"

	bgscr WND_IMAGE_BUF2,,,2
		SET_WND_INFO
		ImgF_PicloadEx "huki.png"
		GetWindowLong WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_IMAGE_BUF2, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_IMAGE_BUF2, WI_HANDLE), 0x000000FF, 192, LWA_COLORKEY | LWA_ALPHA
		imageBuf2W = ginfo_winx
		imageBuf2H = ginfo_winy
		gsel WND_IMAGE_BUF2, -1

	buffer WND_GRID_STRING ,640,150
		SET_WND_INFO
		syscolor 16
		font "メイリオ", 25, 1
		mes "スクリーンショットをここにドラッグアンドドロップ"
		mes "またはホットキー(Altキー＋Zキー同時押し)で
		mes "                                         画像を追加してください"
		mes "セルの画像はウィンドウ外にドラッグで消去できます"
		//640,150

	screen WND_TWITTER, disInfo(2), disInfo(3), 2 | 4
		SET_WND_INFO
		GetWindowLong WND_INFO(WND_TWITTER, WI_HANDLE),-20
		SetWindowLong WND_INFO(WND_TWITTER, WI_HANDLE),-20, stat | $80000
		SetLayeredWindowAttributes WND_INFO(WND_TWITTER, WI_HANDLE), 0x00000000, 255, LWA_ALPHA
		oncmd gosub *TweetWindowProc, WM_LBUTTONDOWN
		oncmd gosub *TweetWindowProc, WM_MOUSEMOVE
		oncmd gosub *TweetWindowProc, WM_COMMAND
		oncmd gosub *TweetWindowProc, WM_DROPFILES

	MakeFFMPEGLogWindow WND_FFMPEG

	buffer WND_TWIBUF, BASE_SIZE_W*ZOOM_MAX, BASE_SIZE_H*ZOOM_MAX
		SET_WND_INFO
	buffer WND_TWIBUF1, BASE_SIZE_W*ZOOM_MAX, BASE_SIZE_H*ZOOM_MAX
		SET_WND_INFO
	buffer WND_TWIBUF2, BASE_SIZE_W*ZOOM_MAX, BASE_SIZE_H*ZOOM_MAX
		SET_WND_INFO
	buffer WND_TWIBUF3, BASE_SIZE_W*ZOOM_MAX, BASE_SIZE_H*ZOOM_MAX
		SET_WND_INFO
	buffer WND_TWIBUF4, BASE_SIZE_W*ZOOM_MAX, BASE_SIZE_H*ZOOM_MAX
		SET_WND_INFO

	buffer WND_CAPTURE, BASE_SIZE_W*ZOOM_MAX, BASE_SIZE_H*ZOOM_MAX
		SET_WND_INFO

	buffer WND_NOIMAGE, BASE_SIZE_W, BASE_SIZE_H
		SET_WND_INFO
		color
		boxf
		color 255, 255, 255
		boxf 8, 8, 791, 471
		color
		font "メイリオ", 130
		pos 100, 150
		mes "No image"

	buffer WND_KANCOLLEBG, BASE_SIZE_W, BASE_SIZE_H
		MakeBackgroundImage WND_KANCOLLEBG, WND_TEMP1

	gsel WND_MAIN

	gosub *loadHistory
	gosub *calcUIScale
	gosub *hotkeyCapSetting
	gosub *twiInit
	gosub *drawMainWindow
	await

	if enableTweetWindow: gsel WND_TWITTER, 1+twiFrontRow
	if enableFFMPEGLogWindow: ShowFFMPEGLog

	if ( restoreMainWindowPos && CheckPosRange(mainWindowPos) ){
		GetWindowRect WND_INFO(WND_MAIN, WI_HANDLE), varptr(rect)
		MoveWindow WND_INFO(WND_MAIN, WI_HANDLE), mainWindowPos(0), mainWindowPos(1), rect(2)-rect(0), rect(3)-rect(1), 1
	}
	if (restoreTweetWindowPos && CheckPosRange(tweetWindowPos) ){
		GetWindowRect WND_INFO(WND_TWITTER, WI_HANDLE), varptr(rect)
		MoveWindow WND_INFO(WND_TWITTER, WI_HANDLE), tweetWindowPos(0), tweetWindowPos(1), rect(2)-rect(0), rect(3)-rect(1), 1
	}

	if initialActivation {
		dialog "艦これ一覧めいかー改二へようこそ！\n自動バージョンチェック機能を有効にしますか？\n※設定からいつでも切り替えができます", 2, "初回起動"
		if (stat != 6){
			autoVersionCheck = FALSE
		}
	}

	gosub *VerCheck

	restoreSSCapFlg = FALSE
	if ( CheckPosRange(ssCap) && ( ssCap(2)-ssCap(0) != 0 || ssCap(3)-ssCap(1) != 0) ){
		MoveWindow WND_INFO(WND_OVERLAY, WI_HANDLE), ssCap(0), ssCap(1), ssCap(2)-SSCap(0), ssCap(3)-ssCap(1), 1
		gosub *CaptureMethodDecide
		if (positionType != PT_ABSOLUTE || (autoSearch^1)){
			ShowWindow WND_INFO(WND_OVERLAY, WI_HANDLE), SW_RESTORE
			dialog "前回起動時の座標を取得しました。\n復元しますか？", 2, "確認"
			dialogStat = stat
			ShowWindow WND_INFO(WND_OVERLAY, WI_HANDLE), SW_HIDE
			if (dialogStat == 6){
				gosub *SSSettingCheck
				restoreSSCapFlg = TRUE
			}
		}
	}

	if (restoreSSCapFlg == FALSE){
		repeat 4: ssCap(cnt) = 0: loop
	}

	gsel WND_MAIN,1+frontRow

	/*
	mode = MODE_OPTION
	optionMode = OPMODE_CAPTURE
	gosub *drawMainWindow
	//*/
	stop

*exit

	if (wparam == WND_GRID){
		mode = MODE_SSCAP
		IsIconicFlg = FALSE
		if (IsIconic(WND_INFO(WMD_MAIN, WI_HANDLE)) != 0){
			ShowWindow WND_INFO(WMD_MAIN, WI_HANDLE), SW_RESTORE
			IsIconicFlg = TRUE
		}
		gosub *drawMainWindow
		gsel WND_MAIN, 1+frontRow
		objprm comboxid, 0
		if (IsIconicFlg){
			ShowWindow WND_INFO(WMD_MAIN, WI_HANDLE), SW_MINIMIZE
		}
		return
	}

	if (wparam == WND_TWITTER){
		gsel WND_TWITTER, -1
		enableTweetWindow = FALSE
		gsel WND_MAIN, 1+frontRow
		if (mode != MODE_OPTION){
			objprm enabletweetwindowcid, 0
		}
		return
	}

	if (wparam == WND_FFMPEG){
		enableFFMPEGLogWindow = 0
		HideFFMPEGLog
		return
	}

	if (confirmCompletion && iparam == 0){
		//iparamが1のときはシャットダウンなので確認せずに終了する
		dialog "艦これ一覧めいかー改二を終了しますか？", 2, "終了確認"
		if (stat != 6):return
	}

	onexit 0
	onerror 0

	if videoCapFlg: gosub *VideoCapEmgStop
	if lateEncodeFlg: gosub *VideoCapEmgStop

	if hdcScreen: DeleteDC hdcScreen
	if dcClient: ReleaseDC dcClient

	DragAcceptFiles WND_INFO(WND_GRID, WI_HANDLE), 0
	DragAcceptFiles WND_INFO(WND_MAIN, WI_HANDLE), 0
	DragAcceptFiles WND_INFO(WND_TWITTER, WI_HANDLE), 0

	if (seqCapF == TRUE){
		KillTimer WND_INFO(WND_MAIN, WI_HANDLE), SeqCapTimerID
		KillTimer WND_INFO(WND_MAIN, WI_HANDLE), SeqCapWriteStatTimerID
	}

	if (hotkeySet == TRUE) {
		UnregisterHotKey WND_INFO(WND_MAIN, WI_HANDLE), HOTKEYID
	}

	ShowWindow WND_INFO(WND_MAIN, WI_HANDLE), SW_RESTORE
	ShowWindow WND_INFO(WND_TWITTER, WI_HANDLE), SW_RESTORE
	await
	gsel WND_MAIN: dim mainWindowPos, 2: mainWindowPos = ginfo(4), ginfo(5)
	gsel WND_TWITTER: dim tweetWindowPos, 2: tweetWindowPos = ginfo(4), ginfo(5)

	if (availableCap){
		sscap(2) -= ssCap(0): sscap(3) -= ssCap(1)
		GetAbsPos ssCap(0), ssCap(1)
		sscap(2) += ssCap(0): sscap(3) += ssCap(1)
	} else {
		repeat 4: ssCap(cnt) = 0: loop
	}
	gosub *dataCheck
	gosub *iniSave

	end
	end

*err_
	onerror 0

	KillTimer WND_INFO(WND_MAIN, WI_HANDLE), VideoCapTimerID
	KillTimer WND_INFO(WND_MAIN, WI_HANDLE), LateEncodeTimerID
	if (apid != -1): pipe2term apid
	if (vpid != -1): pipe2term vpid
	if (mpid != -1): pipe2term mpid
	wait 50
	SafeDelete@kmodule videoFileName+"_v.mp4"
	SafeDelete@kmodule videoFileName+"_a.wav"
	SafeDelete@kmodule videoFileName+".mp4"

	dialog "申し訳ありません。致命的なエラーが発生しました。\nエラー番号 :"+wparam+"\n何度も発生する場合作者kanahironまで報告をお願いします", 1, LONGSOFTNAME
	end
	end

#include "./SubSource/DeclaringVariables.hsp"
#include "./SubSource/DrawWindow.hsp"
#include "./SubSource/iniFileOperation.hsp"
#include "./SubSource/TwitterOperation.hsp"
#include "./SubSource/DrawString.hsp"
#include "./SubSource/Make.hsp"
#include "./SubSource/GetPosition.hsp"
#include "./SubSource/VersionCheck.hsp"
#include "./SubSource/OptionSetting.hsp"
#include "./SubSource/WindowMessageEvent.hsp"
#include "./SubSource/Capture.hsp"
#include "./SubSource/GridWindowOperation.hsp"
#include "./SubSource/VideoCapture.hsp"
#include "./SubSource/SupportWindowOperation.hsp"
